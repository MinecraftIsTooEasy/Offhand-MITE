plugins {
    id 'fml-loom' version '0.1'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "Spongepowered"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name = "ModdedMITE"
        url = ("https://maven.limingzxc.top/repository/maven-public/")
    }
}

configurations {
    fml
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation("it.unimi.dsi:fastutil:8.5.12")
    implementation("com.google.code.gson:gson:2.11.0")

    fml "com.github.MinecraftIsTooEasy:FishModLoader:3.4.2"
    implementation "com.github.MinecraftIsTooEasy:FishModLoader:3.4.2"

    implementation "com.github.MinecraftIsTooEasy:RustedIronCore:1.5.0"
}

def fmlResolvedFile = configurations.fml.files.iterator().next()

loom {
    accessWidenerPath = file("src/main/resources/offhand.accesswidener")
    mergedMinecraftJar()

    mods {
        "${archives_base_name}" {
            sourceSet sourceSets.main
        }
    }
}

loom.setFML(fmlResolvedFile)
dependencies.add("mappings", loom.fmlMCPMappings())

processResources {
    inputs.property "version", project.version

    filesMatching("fml.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

tasks.named('runClient', JavaExec).configure { exec ->
    def user = project.hasProperty('user_name')
        ? project.user_name
        : (project.hasProperty('username') ? project.username : 'Dev')
    exec.setArgs(['--username', user, '--session', '0', '--width', '854', '--height', '480'])
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
